
buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:$springBootVersion"
        classpath "com.netflix.nebula:gradle-ospackage-plugin:4.0.0"
    }
}

plugins {
    id "org.flywaydb.flyway" version "4.0.3"
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'spring-boot'
apply plugin: 'application'
apply plugin: 'nebula.rpm'

ext.java_version = '1.8'

sourceCompatibility = java_version
targetCompatibility = java_version
mainClassName = 'com.xiaoslab.coffee.api.Application'

project.ext {
    majorVersion = 0
    minorVersion = 0
    patchVersion = System.getenv('BUILD_NUMBER')
    
    if (patchVersion == null || patchVersion.isEmpty()) {
        patchVersion = getTimestamp()
    }
    appVersion = String.format('%s.%s.%s', majorVersion, minorVersion, patchVersion)
}

def getTimestamp() {
    def date = new Date()
    def formattedDate = date.format('yyyyMMddHHmmss')
    return formattedDate
}

idea {
    project {
        jdkName = java_version
        languageLevel = java_version
    }
}

jar {
    baseName = 'coffee-api'
    version  = project.appVersion
}

repositories {
    mavenCentral()
}

dependencies {

    // spring
    compile "org.springframework.boot:spring-boot-starter-data-jpa:$springBootVersion"
    compile "org.springframework.boot:spring-boot-starter-web:$springBootVersion"
    compile "org.springframework.boot:spring-boot-starter-jdbc:$springBootVersion"
    compile "org.springframework.boot:spring-boot-starter-actuator:$springBootVersion"
    compile "org.springframework:spring-jdbc:$springVersion"
    compile "org.springframework.security.oauth:spring-security-oauth2:$springOAuthVersion"
    compile "org.springframework.social:spring-social-facebook"
    compile "org.springframework.social:spring-social-google:1.0.0.RC1"

    // swagger
    compile "io.springfox:springfox-swagger2:2.6.0"
    compile "io.springfox:springfox-swagger-ui:2.6.0"

    // jersey
    compile "org.glassfish.jersey.core:jersey-common:2.24"

    // database
    compile "org.flywaydb:flyway-core:$flywayVersion"
    compile "mysql:mysql-connector-java:5.1.39"

    // apache utils
    compile "org.apache.commons:commons-lang3:3.4"

    runtime fileTree (dir: 'config', include: '*.properties')

    // test only
    testCompile "org.springframework.boot:spring-boot-starter-test"
    testCompile "org.apache.httpcomponents:httpclient"

}

task wrapper(type: Wrapper) {
    gradleVersion = '2.13'
}

task flyway {
    Properties props = new Properties()
    props.load(new FileInputStream("config/application.properties"))
    flyway.url=props.getProperty("spring.datasource.url")
    flyway.user=props.getProperty("spring.datasource.username")
    flyway.password=props.getProperty("spring.datasource.password")
    flyway.baselineOnMigrate=true
}

sourceSets {
    main {
        java {
            srcDirs = ['src/main/java']
        }
    }
    test {
        java {
            srcDirs = ['src/test/java']
        }
        resources {
            srcDirs = ['src/test/resources','config']
        }
    }
}

test {
    testLogging {
        events "passed", "skipped", "failed" //, "standardOut", "standardError"
        exceptionFormat = "full"
    }

    def stdout = new LinkedList<String>()
    beforeTest { TestDescriptor td ->
        stdout.clear()
    }
    onOutput { TestDescriptor td, TestOutputEvent toe ->
        stdout.addAll(toe.getMessage().split('(?m)$'))
    }
    afterTest { TestDescriptor td, TestResult tr ->
        if (tr.resultType == TestResult.ResultType.FAILURE && stdout.size() > 0) {
            println()
            println("${td.className}.${td.name} failed, output:")
            stdout.each { print(it) }
        }
    }
    afterSuite { desc, result ->
        if (!desc.parent) { // will match the outermost suite
            println "\nResults: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)"
        }
    }
}

test.dependsOn flywayMigrate

task rpm(type: Rpm) {

    packageName = 'coffee-api'
    summary = 'Xiaoslab Coffee REST API Service'
    version = project.appVersion
    release = project.majorVersion
    license = 'Xiaoslab'
    arch = 'noarch'
    os = LINUX
    group = 'Applications/Xiaoslab'
    vendor = 'Xiaoslab'
    url = 'http://www.xiaoslab.com'

    requires('java')
    requires('httpd')

    def basedir = '/usr/local/xiaoslab/coffee-api'

    from('build/libs') {
        into "${basedir}"
        include 'coffee-api-*.jar'
        fileMode = 0755
    }

    from('config') {
        into "${basedir}/config"
        fileMode = 0755
    }

    from('init.d') {
        into "/etc/init.d"
        fileMode = 0755
    }

}